                      	  NOTS: SAS format modifier
                                SAS 修饰符
===============================================================================  
三个修饰符 & : ~ 【使用修饰符时，变量的输入方式为列表输入】
  
& 读入数据值中单个空格的字符值，遇到两个连续空格或达到定义长度即停止读入。【若
  遇到两个连续空格，停止读入后指针在两个空格之后的位置】

: 可以在变量名后指定输入格式，【但输入方式仍为列表输入，而不是格式输入】

~ 必须与DSD选项一起使用，只是读入引号引起来的数据时不去掉引号，若不与DSD一起使
  用，执行时会被忽略
===============================================================================

===============================================================================
[ Program ] A

* A.dat 数据明细：
* C001  Willam Smith   22OCT1970
* C002  Emily Cooker   01JAN1978
* C003  Geroge Collin  09MAR1968
* C005  Jimmy Cruze    25JUN1972

data saslib.test;
infile extfiles(A);
input Customer_ID $ Name &$20. Birth_Date:date9.;
format Birth_Date date9.;
run;

1）未指定dlm选项，默认分隔符为空格' '，连续分隔符当成一个分隔符处理，且在读入
   时被删除
2）Customer_ID输入方式为列表输入，字符格式，默认长度8，使用分隔符。读取
   customer_id后，指针在customer_id之后的第一个非分隔符列。
2）Name变量指定了&修饰符，允许name数据中间的单个空格，但要求name与后面的
   birth_date间隔2个分隔符
3）读取name后，指针在第三个空格，读取birth_date时需要用:修饰符将输入方式改为列
   表输入，使用分隔符。若不使用:修饰符，而是直接指定date9.格式，输入方式为格式
   化输入，即从当前指针列开始读入9个长度的数据，即'  22OCT19'，无法识别为date9.
   格式，造成缺失。
4）注意读取name和birth_date时的区别，输入Name时未指定:修饰符时因为之前的
   Customer_ID采用列表输入，完成后指针在customer_id之后的第一个非分隔符列，即读
   取name时的起始位置是正确的，这和第3）点是有区别的。另外，读取name时采用:$20.
   也是完全正确的。
===============================================================================

===============================================================================
[ Program ] B

* B.dat 数据明细：
* C001  "Willam Smith"   22OCT1970
* C002  "Emily Cooker"   01JAN1978
* C003  "Geroge Collin"  09MAR1968
* C005  "Jimmy Cruze"    25JUN1972

data saslib.test;
infile extfiles(B);
input Customer_ID $ Name &$quote20. Birth_Date:date9.;
format Birth_Date date9.;
run;

1）输入Name时指定$quotew.格式，该输入格式的作用是读取外面有引号的字符变量，并且
   去掉外边的引号，引号内的字符变量可以包含分隔符等特殊字符。
2）注意，此时不能在infile语句中指定dlm=' ' dsd选项，因为dsd会将连续的分隔符识别
   成缺失
===============================================================================

===============================================================================
[ Program ] C

* C.dat 数据明细：
* C001,"Willam Smith",22OCT1970
* C002,"Emily Cooker",01JAN1978
* C003,"Geroge Collin",09MAR1968
* C005,"Jimmy Cruze",25JUN1972

data saslib.test;
infile extfiles(C) dsd;
input Customer_ID $ Name & $20. Birth_Date date9.;
format Birth_Date date9.;
run;

1）此时使用dsd选项，分隔符为逗号','，读取外面有引号的字符并去掉外面的引号
2）dlm、dlmstr、dsd 选项都是列表输入方式的选项，若input语句中只是单独的格式化输
   入，这些选项不起作用。比如，此时输入Name时需要指定&修饰符或:修饰符，将输入方
   式改为列表输入，若只是$20.格式化输入，sas会从读取customer_id后的指针位置开始
   读入20个长度的字符，包括引号和name中间的空格
3）此时输入Birth_Date时的:修饰符是可选的，因为读取customer_id后的指针位置就在
   birth_date的起始位置。同 [ Program ] A 4)。
===============================================================================